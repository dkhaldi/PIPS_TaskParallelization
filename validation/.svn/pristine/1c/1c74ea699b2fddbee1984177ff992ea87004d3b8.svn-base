*DECK FMTSET
      SUBROUTINE FMTSET(KOP1,KOP2,KOP3)
C
C     ROUTINE TO PRE-SET CUTOFFS FOR FMTGEN.
C
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/FM/GA(17),RPITWO,FMZERO(17),TOL,CUT0S,CUTSM,CUTML
      COMMON/IO/IN,IOUT,IPUNCH
      COMMON/FMCONS/FOUR,ONE,HALF,TWO,ZERO,TEN,TENM9,F20,F42,F500
      DIMENSION F(15)
C
C     COMPUTE PI RELATED CONSTANTS.
      ZERO = DBLE(0)
      ONE = DBLE(1)
      TWO = ONE + ONE
      FOUR = TWO + TWO
      HALF = ONE / TWO
      TEN = FOUR + FOUR + TWO
      TENM9 = TEN**(-9)
      F20 = TWO * TEN
      F42 = TWO*F20 + TWO
      F500 = (FOUR+ONE)*TEN*TEN
      PI=FOUR*DATAN(ONE)
      GA(1)=DSQRT(PI)
      RPITWO=HALF*GA(1)
C
C     FILL GAMMA FUNCTION ARRAY.
      TOL=HALF
      DO 10 I=2,17
      GA(I)=GA(I-1)*TOL
   10 TOL=TOL+ONE
C
C     FILL FMZERO (FOR ARGUMENT OF ZERO).
      TOL=ONE
      FMZERO(1)=ONE
      DO 20 I=2,17
      TOL=TOL+TWO
   20 FMZERO(I)=ONE/TOL
C
C     OBTAIN CUTOFFS.
C
C        CUT0S = CUTOFF, ZERO TO SMALL.
C        CUTSM = CUTOFF, SMALL TO MEDIUM.
C        CUTML = CUTOFF, MEDIUM TO LARGE.
C
C     CUTOS IS OBTAINED FROM KOP1.
      CUT0S=ZERO
      IF(KOP1.NE.0)CUT0S=TEN**(-2*KOP1)
C
C     CUTSM IS OBTAINED FROM KOP2.
C     KOP2 SPECIFIES THE DESIRED ACCURACY OF THE ASSYMPTOTIC
C     EXPANSION.  ROUTINE FMTGEN IS CALLED TO CHECK THE VIABILITY
C     OF THE ASSYMPTOTIC EXPANSION FOR VARIOUS VALUES OF CUTSM.
      TOL=TENM9
      CUTSM=TEN
      CUTML=F42
      IF(KOP2.EQ.0)GO TO 90
      TOL=TEN**(-6-KOP2)
      T=F20
   60 CALL FMTGEN(F,T,1,ICK)
      IF(ICK)80,70,80
   70 T=T-ONE
      IF(T-TEN)90,60,60
   80 CUTSM=T+ONE
C
C     CUTML IS TAKEN FROM KOP3.
   90 CUTML=F42
      IF(KOP3.NE.0)CUTML=DBLE(KOP3)
      RETURN
      END
